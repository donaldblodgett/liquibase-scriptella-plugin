buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
}

apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'com.donaldblodgett.scriptella'
version = '1.0'

repositories {
  jcenter()
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'com.javaforge.scriptella:scriptella-core:1.1',
          'org.liquibase:liquibase-core:3.5.1'
  testCompile 'org.apache.commons:commons-lang3:3.4',
              'org.mockito:mockito-all:1.10.19',
              'org.powermock:powermock-api-mockito:1.6.5',
              'org.powermock:powermock-module-junit4:1.6.3',
              'junit:junit:4.12'
}

eclipse {
  classpath {
    downloadJavadoc = true
    downloadSources = true
  }
}

test.finalizedBy 'jacocoTestReport'
jacocoTestReport {
  reports {
    xml.enabled false
    csv.enabled false
    html.destination "${buildDir}/coverage"
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
         name 'liquibase-scriptella-plugin'
         packaging 'jar'
         description 'A Liquibase plugin to execute Scriptella'
         url 'https://github.com/donaldblodgett/liquibase-scriptella-plugin'

         scm {
           url 'scm:git@github.com:donaldblodgett/liquibase-scriptella-plugin.git'
           connection 'scm:git@github.com:donaldblodgett/liquibase-scriptella-plugin.git'
           developerConnection 'scm:git@github.com:donaldblodgett/liquibase-scriptella-plugin.git'
         }

         licenses {
           license {
             name 'The Apache Software License, Version 2.0'
             url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
             distribution 'repo'
           }
         }

         developers {
           developer {
             id 'donaldblodgett'
             name 'Donald Blodgett'
           }
         }
       }
    }
  }
}